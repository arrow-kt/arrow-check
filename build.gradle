buildscript {
    ext {
        def customProperties = new Properties()
        def customPropertiesURL = new URL("$PROPERTIES")
        def customPropertiesStream = customPropertiesURL.openStream()
        customProperties.load(customPropertiesStream)
        customProperties.each { key, value -> set(key, value) }
        apply from: "$COMMON_SETUP"
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlinx:atomicfu-gradle-plugin:0.15.2"
    }
}

plugins {
    id "org.jetbrains.kotlin.multiplatform" version "$KOTLIN_VERSION" apply false
    id "org.jlleitschuh.gradle.ktlint" version "$KTLINT_GRADLE_VERSION"
    id "org.jetbrains.dokka" version "$DOKKA_VERSION"
}

apply from: "$ROOT_PROJECT"

subprojects { 

    repositories {
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url 'https://jitpack.io' }
    }

    dokka {
        outputFormat = 'jekyll'
        outputDirectory = "${rootDir}/build/apidocs"
        configuration {
            includes = ['README.md']
            reportUndocumented = false
            sourceLink {
                path = "src/main/kotlin"
                url = "https://github.com/arrow-kt/arrow/blob/main/${relativeProjectPath('src/main/kotlin')}"
                lineSuffix = "#L"
            }
        }
    }
}
